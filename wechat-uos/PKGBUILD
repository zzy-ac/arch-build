# Maintainer: DuckSoft <realducksoft at gmail dot com>
# Maintainer: sukanka <su975853527 at gmail dot com>
# Contributor: sihuan <sihuan at sakuya.love>
# Contributor: Nick Cao <nickcao at nichi dot co>
# Contributor: Xuanwo <xuanwo@archlinuxcn.org>

pkgname=wechat-uos
_pkgname=wechat
pkgver=1.0.0.236
pkgrel=2
pkgdesc="微信官方原生桌面版 WeChat desktop"
arch=('x86_64' 'aarch64')
url="https://weixin.qq.com/"
license=('ISC')
depends=( bubblewrap lsb-release openssl-1.1)
optdepends=('scrot: For in-app screenshot')
source=(
  # Bind files like os-release to wechat-uos works.
  "wechat.sh"
  "wechat-uos.desktop"
  # We need the license key from uos to make wechat-uos works.
  #
  # from https://aur.archlinux.org/packages/com.tencent.weixin
  "license.tar.gz"
)
source_x86_64=("${_pkgname}-${pkgver}-x86_64.deb::https://pro-store-packages.uniontech.com/appstore/pool/appstore/c/com.tencent.wechat/com.tencent.wechat_1.0.0.236_amd64.deb"
)
sha512sums=('334a5185943f149f069611e5a668cb524f80892928be85ef0ecf5d83bbee442d7ac2fdd50cc8029d9040f8df66fd43145243e5c22ec99206d93c1481690ea7b4'
            '68d31d9e7a6ee14d5cc330acf55d9905be86c4ac187279e33494bc479a60c078b38eef0fa7833ba9fda2b14978f212d8647dd609b6f3037b0133701fe8f5e72b'
            'cea76fd37d987c6a6c21f18b51def9d06aba53f0d7e9a32a5cf6dcd578be152034696aac8a50ac08b9c2e7b5d8c98dbf13b835abe5e0cca28a2bb0b222cdea00')
sha512sums_x86_64=('b119a80261102ddb1997c3513becb749718a16d970352a2f12beb86d01b090b39a8e2cf8f4175ea52ae0fa1319441ba8e4ac72ace7b20e9a4e8ec9f9c34a9b37')

prepare(){
  tar -Jxvf data.tar.xz -C "${srcdir}"
  mkdir -p ${srcdir}/usr/
  mv ${srcdir}/opt/apps/com.tencent.wechat/entries/ ${srcdir}/usr/share/
  mv ${srcdir}/usr/share/applications/com.tencent.wechat.desktop ${srcdir}/usr/share/applications/wechat.desktop
  sed -i 's|^Icon=.*|Icon=wechat|g;s|Exec=.*|Exec=wechat-uos %U|g' ${srcdir}/usr/share/applications/wechat.desktop
}

package(){
    cp -rf ${srcdir}/usr ${pkgdir}
    install -Dm755 ${srcdir}/wechat.sh  ${pkgdir}/usr/bin/wechat-uos
    install -d ${pkgdir}/usr/lib
    mv ${srcdir}/opt/apps/com.tencent.wechat/files/ ${pkgdir}/usr/lib/${pkgname}
    install -m 755 -d ${pkgdir}/usr/lib/license
    install -m 755 -d ${pkgdir}/usr/share/${pkgname}
    cp -r license/etc ${pkgdir}/usr/share/${pkgname}
    cp -r license/var ${pkgdir}/usr/share/${pkgname}

}
# vim: ts=2 sw=2 et:
